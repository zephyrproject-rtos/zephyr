# SPDX-License-Identifier: Apache-2.0

# Andes toolchain naming rule:
#   nds[32|64]-[elf|linux]-[mculib|newlib|glibc]-[v5e|v5|v5f|v5d]
# The kernel configuration must match the toolchain for ABI
cmake_path(GET ANDES_TOOLCHAIN_PATH FILENAME TOOLCHAIN_BASENAME)

string(REPLACE "-" ";" TOOLCHAIN_PARTS "${TOOLCHAIN_BASENAME}")
list(GET TOOLCHAIN_PARTS 0 TOOLCHAIN_ARCH)  # nds32le / nds64le
list(GET TOOLCHAIN_PARTS 3 TOOLCHAIN_ISA)   # v5e / v5 / v5f / v5d

# Check toolchain architecture

# TOOLCHAIN_ARCH == [nds32le|nds64le]  <=>  CONFIG_64BIT=[n|y]
if((TOOLCHAIN_ARCH STREQUAL "nds32le" AND CONFIG_64BIT) OR
   (TOOLCHAIN_ARCH STREQUAL "nds64le" AND NOT CONFIG_64BIT))
  message(FATAL_ERROR "${TOOLCHAIN_BASENAME} (${TOOLCHAIN_ARCH}) mismatches "
    "CONFIG_64BIT=${CONFIG_64BIT}")
endif()

# Check toolchain ISA

# TOOLCHAIN_ISA == [v5e|other]  <=>  CONFIG_RISCV_ISA_RV32E=[y|n]
if((TOOLCHAIN_ISA STREQUAL "v5e" AND NOT CONFIG_RISCV_ISA_RV32E) OR
   (NOT TOOLCHAIN_ISA STREQUAL "v5e" AND CONFIG_RISCV_ISA_RV32E))
  message(FATAL_ERROR "${TOOLCHAIN_BASENAME} (${TOOLCHAIN_ISA}) mismatches "
    "CONFIG_RISCV_ISA_RV32E=${CONFIG_RISCV_ISA_RV32E}")
endif()

# TOOLCHAIN_ISA == [v5e,v5|v5f,v5d]  <=>  CONFIG_FPU=[n|y]
if(((TOOLCHAIN_ISA STREQUAL "v5" OR TOOLCHAIN_ISA STREQUAL "v5e") AND CONFIG_FPU) OR
   ((TOOLCHAIN_ISA STREQUAL "v5f" OR TOOLCHAIN_ISA STREQUAL "v5d") AND NOT CONFIG_FPU))
  message(FATAL_ERROR "${TOOLCHAIN_BASENAME} (${TOOLCHAIN_ISA}) mismatches "
    "CONFIG_FPU=${CONFIG_FPU}")
endif()

# TOOLCHAIN_ISA == v5f,v5d  <=>  CONFIG_FLOAT_HARD=y
if((TOOLCHAIN_ISA STREQUAL "v5f" OR TOOLCHAIN_ISA STREQUAL "v5d") AND NOT CONFIG_FLOAT_HARD)
  message(FATAL_ERROR "${TOOLCHAIN_BASENAME} (${TOOLCHAIN_ISA}) mismatches "
    "CONFIG_FLOAT_HARD=${CONFIG_FLOAT_HARD}")
endif()

# TOOLCHAIN_ISA == [v5f|v5d]  <=>  CONFIG_CPU_HAS_FPU_DOUBLE_PRECISION=[n|y]
if((TOOLCHAIN_ISA STREQUAL "v5f" AND CONFIG_CPU_HAS_FPU_DOUBLE_PRECISION) OR
   (TOOLCHAIN_ISA STREQUAL "v5d" AND NOT CONFIG_CPU_HAS_FPU_DOUBLE_PRECISION))
  message(FATAL_ERROR "${TOOLCHAIN_BASENAME} (${TOOLCHAIN_ISA}) mismatches "
    "CONFIG_CPU_HAS_FPU_DOUBLE_PRECISION=${CONFIG_CPU_HAS_FPU_DOUBLE_PRECISION}")
endif()
